ext {
    springVersion = "4.1.5.RELEASE"
    springBootVersion = "1.2.3.RELEASE"
    jacksonVersion = "2.5.1"
}

buildscript {
    repositories {
        maven { url "https://repo.spring.io/libs-release" }
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "spring-boot"

    group = "net.kiel.cafe"
    version = "1.0-SNAPSHOP"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.14.+'
        ['jackson-core', 'jackson-databind', 'jackson-annotations'].each {
            compile "com.fasterxml.jackson.core:$it:$jacksonVersion"
        }


        testCompile "org.springframework:spring-test:$springVersion"
        testCompile "junit:junit:4.11"
        testCompile "org.codehaus.groovy:groovy-all:2.4.1"
        testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
    }
}

project(':cafe-core') {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.boot:spring-boot-starter-jdbc"
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.security:spring-security-core:4.0.0.RELEASE"
        compile "org.apache.tomcat:tomcat-jdbc:8.0.+"
        compile "org.hibernate:hibernate-validator:5.1.3.Final"

        compile "mysql:mysql-connector-java:5.1.+"
        runtime "com.h2database:h2:1.4.181"
    }

    bootRepackage.enabled = false
}

project(':cafe-web') {
    dependencies {
        compile project(':cafe-core')
         
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-security"

        // View
        compile "org.springframework.boot:spring-boot-starter-freemarker"
        compile "org.springframework.boot:spring-boot-starter-thymeleaf"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    }

    // for delegate system properties to bootRun
    tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
        systemProperties = System.properties
    }
}