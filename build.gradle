ext {
    springVersion = "4.1.5.RELEASE"
    jacksonVersion = "2.5.1"
}

buildscript {
    repositories {
        maven { url "https://repo.spring.io/libs-release" }
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "groovy"

    group = "net.kiel.cafe"
    version = "1.0-SNAPSHOP"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "spring-boot"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.14.+'
        ['jackson-core', 'jackson-databind', 'jackson-annotations'].each {
            compile "com.fasterxml.jackson.core:$it:$jacksonVersion"
        }

        runtime "org.codehaus.groovy:groovy-all:2.4.1"
        
        testCompile "org.springframework:spring-test:$springVersion"
        testCompile "junit:junit:4.11"
    }
}

project(':cafe-core') {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.boot:spring-boot-starter-jdbc"
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "mysql:mysql-connector-java:5.1.+"
        compile "org.apache.tomcat:tomcat-jdbc:8.0.+"
        compile "org.hibernate:hibernate-validator:5.1.3.Final"
        runtime "com.h2database:h2:1.4.181"

        testCompile "org.spockframework:spock-spring:0.7-groovy-2.0"
    }

    bootRepackage.enabled = false
}

project(':cafe-cafeweb') {
    dependencies {
        compile project(':cafe-core')
         
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-freemarker"
    }

    // for delegate system properties to bootRun
    tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
        systemProperties = System.properties
    }
}

project(':cafe-sectionweb') {
    dependencies {
        compile project(':cafe-core')
 
        // Spring Boot
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-velocity"
        compile "org.springframework.boot:spring-boot-starter-security"
    }

    // for delegate system properties to bootRun
    tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
        systemProperties = System.properties
    }
}

project(':cafe-api') {
   dependencies {
        compile project(':cafe-core')
     
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
        // Swagger
        compile "com.mangofactory:swagger-springmvc:1.0.0"
    }

    // for delegate system properties to bootRun
    tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
        systemProperties = System.properties
    }
}